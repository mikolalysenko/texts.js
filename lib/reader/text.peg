Document
  = blocks:(b:Block (EOL / EOF) { return b; })*
  { return ['text'].concat(blocks); }

Block
  = Heading
  / Verbatim
  / Divider
  / Para

/*
  / Formula
  / Quote
  / Bulleted
  / Numbered
  / Image
  / Note
  / Table
*/

Inline
  = Code
  / Strong
  / Emph
  / Label
  / Url
  / Plain

/******************************* Basic elements *************************************/

EOF
  = !.

EOL
  = '\n' / '\r' '\n'?

Break
  = '  ' EOL
  { return ['break', '\n']; }

Tildes
  = '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'

Dashes
  = '--------------------------------------------------------------------------------'

/*********************************** Blocks *****************************************/

/*** Headings ***/

Heading
  = Heading1
  / Heading2
  / Heading3

Heading1
  = inlines:Inline+ EOL '='+ EOL
  { return ['heading', { 'level': 1 }].concat(inlines); }

Heading2
  = inlines:Inline+ EOL '-'+ EOL
  { return ['heading', { 'level': 2 }].concat(inlines); }

Heading3
  = '### ' inlines:Inline+ EOL
  { return ['heading', { 'level': 3 }].concat(inlines); }

/*** Verbatim ***/

Verbatim
  = Tildes EOL inlines:(VerbatimLine / VerbatimBreak)* EOL Tildes EOL
  { return ['verbatim', { 'level': 0 }].concat(inlines); }

VerbatimLine
  = !Tildes chars:(!EOL c:. { return c; })+
  { return ['plain', chars.join('')]; };

VerbatimBreak
  = !(EOL Tildes) EOL
  { return ['break', '\n']; }

/*** Others ***/

Divider
  = Dashes EOL
  { return ['divider', { 'level': 0 }]; }  

Para
  = inlines:(Inline / Break)* EOL
  { return ['para', { 'level': 0 }].concat(inlines);}

/************************************ Inlines ***************************************/

Code
  = '`' chars:(!EOL !'`' c:. { return c; })+ '`'
  { return ['code', chars.join('')]; };

Strong
  = '**' chars:(!EOL !'**' c:. { return c; })+ '**'
  { return ['strong', chars.join('')]; };

Emph
  = '*' chars:(!EOL !'*' c:. { return c; })+ '*'
  { return ['emph', chars.join('')]; };

Label
  = '[' chars:(!EOL !']' c:. { return c; })+ ']'
  { return ['label', chars.join('')]; };

Url
  = '<' chars:(!EOL !'>' c:. { return c; })+ '>'
  { return ['url', chars.join('')]; };

Plain
  = chars:(!EOL !'`' !'*' !'[' !'<' c:. { return c; })+
  { return ['plain', chars.join('')]; };
